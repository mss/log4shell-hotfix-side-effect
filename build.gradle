plugins {
    id 'application'
}

def LOG4J_HOTFIX = true
def LOG4J_VERSION = '2.14.1'

version '1.0-SNAPSHOT'
group 'de.silpion.log4shell.hotfix.side.effect'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-api:' + LOG4J_VERSION
    implementation 'org.apache.logging.log4j:log4j-core:' + LOG4J_VERSION
}

compileJava {
    options.release = 11
}

application {
    mainClass = [ project.group, 'Main' ].join('.')
}

task hotfix(type: Exec) {
    def dir = [ buildDir, 'install', project.name, 'lib' ].join('/')
    executable = "zip"
    workingDir dir
    args "-q", "-d", "log4j-core-${LOG4J_VERSION}.jar", "org/apache/logging/log4j/core/lookup/JndiLookup.class"
}
if (LOG4J_HOTFIX) {
    installDist.configure {
        finalizedBy tasks.named('hotfix')
    }
}